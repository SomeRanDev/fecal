package fecal;

import fecal.data.FolderNames;
import fecal.data.GenerateBuildCommandData;
import fecal.data.GenerateExecuteCommandData;
import fecal.data.GenerateTestArgumentsData;

/**
	Runs Fecal tests.

	`testsDirectory` is the directory that contains all the test directory folders.

	`folderNames` allows for the configuration of the names of the folders generated by the
	tests in Fecal. This class supports `@:structInit`, and the default values can be used 
	by passing `null` or `{}`.

	`generateTestArguments` is a callback that configures the arguments passed to the Haxe compiler
	for every test that is detected. Its value is a list of the arguments applied to the Haxe 
	executable. The user must also include the `.hxml` provided using `fecal.Fecal.GenerateTestArgumentsData`'s
	`hxml.absolutePath`.

	```haxe
	final result = Fecal.test(
		// The path to the folder containing the tests
		"tests",

		// The names of the folders read/generated
		{
			output: "output",
			intendedOutput: "intended",
			build: "bin"
		},

		// This function is run for every test prior to execution.
		function(data: GenerateTestArgumentsData) {
			return [
				"--no-opt",
				'-js ${data.outputDirectory}/Main.js',
				'"${data.hxmlFile.absolutePath}"'
			];
		}
	);

	switch(result) {
		case Ok(PassedTests(testCount)): {
			Sys.println("All tests passed!");
		}
		case _: {
			// Handle the various possible fail states...
		}
	}
	```
**/
class Fecal {
	public static function test(
		testsDirectory: String,
		folderNames: Null<FolderNames> = null,
		generateTestArguments: Null<(GenerateTestArgumentsData) -> Array<String>> = null,
		generateBuildCommand: Null<(GenerateBuildCommandData) -> String> = null,
		generateExecuteCommand: Null<(GenerateExecuteCommandData) -> Null<String>> = null
	): Error<TesterResult> {
		final folderNames = folderNames ?? {};
		final generateTestArguments = generateTestArguments ?? function(data) {
			return [
				'-js ${data.outputDirectory}/Main.js',
				'"${data.hxmlFile.absolutePath}"'
			];
		}

		final tester = new Tester(
			testsDirectory,
			folderNames,
			generateTestArguments,
			generateBuildCommand,
			generateExecuteCommand
		);
		return tester.run();
	}
}
