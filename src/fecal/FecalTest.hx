package fecal;

import fecal.data.Arguments;
import fecal.data.FolderNames;

/**
	To configure the behavior of how tests run in your project, you must create a child
	of this class, override the necessary functions, then pass an instance of the child
	class to `fecal.Fecal.test`.
**/
class FecalTest {
	/**
		The relative directory that contains all the tests.
		This is configured in the constructor.
	**/
	public var testsDirectory(default, null): String;

	/**
		Allows for the configuration of the names of the folders generated by the tests
		in Fecal. This class supports `@:structInit`, and the default values can be used 
		by assigning `{}`.
	**/
	public var folderNames(default, null): FolderNames;

	/**
		Constructs a test for a project.

		The arguments directly assign to the properties with the same names.
	**/
	public function new(testsDirectory: String, folderNames: Null<FolderNames> = null) {
		this.testsDirectory = testsDirectory;
		this.folderNames = folderNames ?? {};
	}

	/**
		Override to configure the arguments passed to the Haxe compiler for each test.
	**/
	public function generateHaxeCompilationArguments(
		outputDirectory: String,
		testDirectory: String,
		hxmlFile: {
			name: String,
			absolutePath: String
		},
		arguments: Arguments,
	): Array<String> {
		return [
			'"${hxmlFile.absolutePath}"'
		];
	}

	/**
		Generates the command to build an executable format from the Haxe command output.
		This only runs if all the Haxe output comparison tests pass.

		If `null` is returned, no build command is run.
	**/
	public function generateBuildCommand(
		outputDirectory: String,
		buildDirectory: String,
		testDirectory: String,
		arguments: Arguments,
	): Null<String> {
		return null;
	}

	/**
		Generates the command to execute the result from the build phase (or Haxe phase if
		the build phase is skipped).

		If `null` is returned, no execution command is run.
	**/
	public function generateExecuteCommand(
		outputDirectory: String,
		buildDirectory: String,
		testDirectory: String,
		arguments: Arguments,
	): Null<String> {
		return null;
	}
}
